apply from: 'svg.gradle'

apply plugin: 'org.kordamp.markdown.convert'

project.ext {
	mdDocDir = file("$buildDir/doc/md")
}


// https://github.com/aalmiray/markdown-gradle-plugin
markdownToHtml{
	sourceDir = mdDocDir
	outputDir = htmlDocDir
}

task genIndexMd(){
	group = "documentation"
	description = "Generate markdown index and default patch markdown"
	doLast{
		markdownToHtml.sourceDir.mkdirs()
		def myFile = file("$mdDocDir/index.md")
		myFile.delete()
		def header = file("doc/index.md").text
	 	def content = "\n\n"
		fileTree('lib').include('**/*-help.pd').each {File f ->
			def absName = f.name.take(f.name.lastIndexOf('.'))
			content += "* [${f.name}](${absName}.md)\n"
			
			def indexFile = file("$mdDocDir/${absName}.md")
			indexFile.delete()
			indexFile << "![${absName} image](${absName}.svg)"
		}
		myFile << header + content
	}
}

task prepareMarkdowns(type: Copy, dependsOn: genIndexMd){
	group = "documentation"
	description = "Collect markdown files"
	from('lib')
	into(mdDocDir)
	include "*.md"
}

markdownToHtml.dependsOn += prepareMarkdowns

task genHtml(dependsOn: [markdownToHtml, patchesToSVG]){
	group = "documentation"
	description = "Generate HTML documentation"
}
